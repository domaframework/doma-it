plugins {
    id 'com.diffplug.gradle.spotless' version '3.27.2'
    id 'java'
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

version = '2.28.1-SNAPSHOT' 
ext.dependentVersion = '2.28.0'
ext.domaResources = ['doma.compile.config', 'META-INF/**/*.sql', 'META-INF/**/*.script']

task copyDomaResources(type: Sync) {
    from sourceSets.main.resources.srcDirs
    into compileJava.destinationDir
    include domaResources
}

compileJava {
    dependsOn copyDomaResources
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Adoma.domain.converters=org.seasar.doma.it.domain.DomainConverterProvider']
}

processResources {
    exclude domaResources
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-proc:none"]
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Doma-IT', 'Implementation-Version': archiveVersion
    }
}

test {
    systemProperty 'url', jdbcUrl
    systemProperty 'user', jdbcUser
    systemProperty 'password', jdbcPassword
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    annotationProcessor "org.seasar.doma:doma:${dependentVersion}"
    implementation "org.seasar.doma:doma:${dependentVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testRuntimeOnly 'org.hsqldb:hsqldb:2.3.2'
    testRuntimeOnly 'com.h2database:h2:1.3.175'
    testRuntimeOnly 'mysql:mysql-connector-java:5.1.29'
    testRuntimeOnly 'org.postgresql:postgresql:9.3-1100-jdbc41'
}

apply from:'eclipse.gradle'
apply plugin: 'idea'
