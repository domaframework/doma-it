plugins {
    id 'java'
    id 'com.diffplug.eclipse.apt' version '3.24.0'
    id 'com.diffplug.spotless' version '5.4.0'
    id 'org.seasar.doma.compile' version '1.1.0'
}

def domaVersion = project.findProperty('domaVersion') ?: "2.+"

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Adoma.domain.converters=org.seasar.doma.it.domain.DomainConverterProvider']
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-proc:none"]
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Doma-IT', 'Implementation-Version': archiveVersion
    }
}

test {
    systemProperty 'url', jdbcUrl
    systemProperty 'user', jdbcUser
    systemProperty 'password', jdbcPassword
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    annotationProcessor "org.seasar.doma:doma-processor:${domaVersion}"
    implementation "org.seasar.doma:doma-core:${domaVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testRuntimeOnly 'com.h2database:h2:1.4.200'
    testRuntimeOnly 'mysql:mysql-connector-java:8.0.21'
    testRuntimeOnly 'org.postgresql:postgresql:42.2.16'
    testRuntimeOnly 'com.microsoft.sqlserver:mssql-jdbc:8.4.1.jre14'
}

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                classpath.entries.removeAll { it.path == '.apt_generated' }
            }
            withXml { provider ->
                def node = provider.asNode()
                node.appendNode( 'classpathentry', [ kind: 'src', output: 'bin/main', path: '.apt_generated'])
            }
        }
    }
    jdt {
        javaRuntimeName = 'JavaSE-1.8'
    }
}
