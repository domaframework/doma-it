apply plugin: 'java'

version = '2.20.1-SNAPSHOT' 
ext.dependentVersion = '2.20.0'
ext.domaResourcesDir = "${buildDir}/tmp/doma-resources"

task extractDomaResources(type: Copy, dependsOn: processResources)  {
    from processResources.destinationDir
    include 'doma.compile.config'
    include 'META-INF/**/*.sql'
    include 'META-INF/**/*.script'
    into domaResourcesDir
}

task copyDomaResources(type: Copy, dependsOn: extractDomaResources)  {
    from domaResourcesDir
    into compileJava.destinationDir
}

compileJava {
    dependsOn copyDomaResources
    inputs.dir domaResourcesDir
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Adoma.domain.converters=org.seasar.doma.it.domain.DomainConverterProvider']
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-proc:none"]
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Doma-IT', 'Implementation-Version': version
    }
}

test {
    systemProperty 'url', jdbcUrl
    systemProperty 'user', jdbcUser
    systemProperty 'password', jdbcPassword
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    annotationProcessor "org.seasar.doma:doma:${dependentVersion}"
    implementation "org.seasar.doma:doma:${dependentVersion}"
    testImplementation 'junit:junit:4.11'
    testRuntimeOnly 'org.hsqldb:hsqldb:2.3.2'
    testRuntimeOnly 'com.h2database:h2:1.3.175'
    testRuntimeOnly 'mysql:mysql-connector-java:5.1.29'
    testRuntimeOnly 'org.postgresql:postgresql:9.3-1100-jdbc41'
}

apply from:'eclipse.gradle'

apply plugin: 'idea'
idea.module.inheritOutputDirs = true
