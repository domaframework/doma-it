plugins {
    id 'com.diffplug.eclipse.apt' version '3.22.0'
    id 'com.diffplug.gradle.spotless' version '3.27.2'
    id 'java'
    id "net.researchgate.release" version '2.8.1'
    id 'org.seasar.doma.compile' version '1.0.0'
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

ext.domaVersion = '2.29.0'

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Adoma.domain.converters=org.seasar.doma.it.domain.DomainConverterProvider']
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-proc:none"]
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Doma-IT', 'Implementation-Version': archiveVersion
    }
}

test {
    systemProperty 'url', jdbcUrl
    systemProperty 'user', jdbcUser
    systemProperty 'password', jdbcPassword
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    annotationProcessor "org.seasar.doma:doma:${domaVersion}"
    implementation "org.seasar.doma:doma:${domaVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testRuntimeOnly 'org.hsqldb:hsqldb:2.3.2'
    testRuntimeOnly 'com.h2database:h2:1.3.175'
    testRuntimeOnly 'mysql:mysql-connector-java:5.1.29'
    testRuntimeOnly 'org.postgresql:postgresql:9.3-1100-jdbc41'
}

release {
    newVersionCommitMessage = '[Gradle Release Plugin] - [skip ci] new version commit: '
}

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                classpath.entries.removeAll { it.path == '.apt_generated' }
            }
            withXml { provider ->
                def node = provider.asNode()
                node.appendNode( 'classpathentry', [ kind: 'src', output: 'bin/main', path: '.apt_generated'])
            }
        }
    }
    jdt {
        javaRuntimeName = 'JavaSE-1.8'
    }
}
